buildscript {
	ext {
		springBootVersion = '2.3.4.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

// tasks: spotbugsMain, spotbugsTest, spotbugsIntegrationTest or "check" for all
plugins {
	id "com.github.spotbugs" version "4.5.0"
	// ./gradlew dependencyUpdates
	id "com.github.ben-manes.versions" version "0.33.0"
	id 'com.github.node-gradle.node' version '2.2.4'
	id 'war'
}

apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
// run jacoco via gradle jacocoTestReport, report in build/reports
apply plugin: 'jacoco'

group = 'org.tightblog'
version = '3.7.2'
sourceCompatibility = 11
targetCompatibility = 11
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
	mavenCentral()
}

sourceSets {
	test {
		java {
			srcDir 'src/test/java'
			exclude '**/*IT.java'
		}
	}
	// https://docs.gradle.org/current/userguide/java_testing.html#sec:configuring_java_integration_tests
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/test/java')
			include '**/*IT.java'
		}
		resources.srcDir file('src/test/resources')
	}
}

static def standardCompile(options) {
    options.compilerArgs += ["-proc:none", "-Xlint:unchecked"]
}

compileJava {
    standardCompile(options)
}

compileTestJava {
    standardCompile(options)
}

compileIntegrationTestJava {
    standardCompile(options)
}

project.ext.buildNumber = java.time.Instant.now().getEpochSecond()

def standardProcessResources(obj) {
    obj.filesMatching(['application.properties', 'application-tbcustom.properties']) {
        expand(version: version,
               buildNumber: buildNumber,
               buildDir: buildDir
        )
    }
}

processResources {
    standardProcessResources(processResources)
}

processTestResources {
    standardProcessResources(processTestResources)
}

processIntegrationTestResources {
    standardProcessResources(processIntegrationTestResources)
}

configurations {
	integrationTestImplementation.extendsFrom implementation, testImplementation
	integrationTestRuntimeOnly.extendsFrom runtimeOnly
	// using log4j2 instead of logback
	// global excludes: http://mrhaki.blogspot.com/2012/10/gradle-goodness-exclude-transitive.html
	all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
}

task integrationTest(type: Test) {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
	mustRunAfter test
}

check.dependsOn integrationTest

checkstyleMain.enabled = false

checkstyle {
	toolVersion = "8.9"
	config = resources.text.fromFile("$rootProject.projectDir/etc/buildconfig/checkstyle.xml", 'UTF-8')
	showViolations = true
	ignoreFailures = false
}

spotbugsMain.enabled = false

spotbugs {
	excludeFilter = file("$rootProject.projectDir/etc/buildconfig/spotbugsExcludes.xml")
}


dependencies {
	implementation("org.springframework.boot:spring-boot-starter-data-jpa") {
		exclude group: "org.hibernate", module: "hibernate-entitymanager"
	}
	implementation('org.eclipse.persistence:org.eclipse.persistence.jpa:2.7.4')
	implementation('org.springframework.boot:spring-boot-starter-security')
	implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('org.springframework.boot:spring-boot-starter-mail')
	implementation('org.springframework.boot:spring-boot-starter-log4j2')
	implementation('org.springframework.boot:spring-boot-starter-validation')
	implementation('org.springframework.security:spring-security-taglibs:5.4.0')
	implementation('org.springframework.mobile:spring-mobile-device:1.1.5.RELEASE')
	implementation('org.apache.commons:commons-lang3:3.11')
	implementation('org.apache.commons:commons-text:1.9')
	implementation('org.apache.lucene:lucene-analyzers-common:8.6.2')
	implementation('org.apache.lucene:lucene-queryparser:8.6.2')
	implementation('org.apache.tiles:tiles-jsp:3.0.8')
	implementation('javax.servlet:jstl:1.2')
	implementation('commons-validator:commons-validator:1.7')
	implementation('org.jsoup:jsoup:1.13.1')
	implementation('com.github.ben-manes.caffeine:caffeine:2.8.5')
	implementation('org.thymeleaf:thymeleaf-spring4:3.0.11.RELEASE')
	implementation('org.jboss.aerogear:aerogear-otp-java:1.0.0')
	implementation('com.atlassian.commonmark:commonmark:0.15.2')
	implementation('commons-fileupload:commons-fileupload:1.4')
	implementation('org.apache.derby:derbyclient:10.14.2.0')

	testImplementation('org.springframework.boot:spring-boot-starter-test')

	runtimeOnly('org.apache.derby:derby:10.14.2.0')

	// PostgreSQL JDBC jar, can remove if using other databases.
	runtimeOnly('org.postgresql:postgresql')

	providedCompile('org.apache.tomcat.embed:tomcat-embed-jasper:9.0.11')
	// https://docs.spring.io/spring-boot/docs/current/reference/htmlsingle/#howto-create-a-deployable-war-file
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
}

def uiDir = 'src/client'

node {
	version = '14.15.1'
	npmVersion = '6.14.9'
	download = true
	// distBaseUrl = Custom artifactory location here for node/npm.
}

task npmInstallBloggerUI(type: NpmTask) {
	args = ['install']
	execOverrides {
		it.workingDir = uiDir
	}
}

task npmBuildBloggerUI(type: NpmTask) {
	dependsOn npmInstallBloggerUI
	args = ['run','build']
	execOverrides {
		it.workingDir = uiDir
	}
}

task copyUi(type: Copy) {
	dependsOn npmBuildBloggerUI
	into "src/main/webapp"

	into("tb-ui2") {
		from "${uiDir}/dist"
	}
}

bootWar.dependsOn copyUi
